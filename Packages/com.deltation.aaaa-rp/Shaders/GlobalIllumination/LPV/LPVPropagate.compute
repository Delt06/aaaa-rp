#pragma kernel CS

#pragma multi_compile_local _ BLOCKING_POTENTIAL
#pragma multi_compile_local _ SKY_OCCLUSION
#pragma multi_compile_local _ FIRST_STEP

// Sources:
// - https://github.com/mafian89/Light-Propagation-Volumes/blob/master/shaders/propagation.frag
// - https://ericpolman.com/2016/06/28/light-propagation-volumes/

#define SIDE_FACE_CONTRIBUTION 1
#define DIRECT_FACE_CONTRIBUTION 1

#include_with_pragmas "Packages/com.deltation.aaaa-rp/ShaderLibrary/Bindless.hlsl"

#include "Packages/com.deltation.aaaa-rp/ShaderLibrary/Core.hlsl"
#include "Packages/com.deltation.aaaa-rp/ShaderLibrary/Lighting.hlsl"

#define THREAD_GROUP_SIZE 4

TYPED_TEXTURE3D(LPV_CHANNEL_T, _SourceRedSH);
TYPED_TEXTURE3D(LPV_CHANNEL_T, _SourceGreenSH);
TYPED_TEXTURE3D(LPV_CHANNEL_T, _SourceBlueSH);
TYPED_TEXTURE3D(float, _SourceSkyOcclusion);

RWTexture3D<LPV_CHANNEL_T> _DestinationRedSH;
RWTexture3D<LPV_CHANNEL_T> _DestinationGreenSH;
RWTexture3D<LPV_CHANNEL_T> _DestinationBlueSH;
RWTexture3D<float>         _DestinationSkyOcclusion;

float _Intensity;
float _SkyOcclusionPropagationIntensity;
float _OcclusionAmplification;

TYPED_TEXTURE3D(LPV_CHANNEL_T, _BlockingPotentialSH);

#define DIRECTIONS_COUNT 6
static const float3 directions[DIRECTIONS_COUNT] =
{float3(0, 0, 1), float3(0, 0, -1), float3(1, 0, 0), float3(-1, 0, 0), float3(0, 1, 0), float3(0, -1, 0)};

// right up
int2 cellSides[4] = {int2(1.0, 0.0), int2(0.0, 1.0), int2(-1.0, 0.0), int2(0.0, -1.0)};

float3 GetEvalSideDirection(int index, int3 orientation)
{
    const static float smallComponent = 0.4472135; // 1 / sqrt(5)
    const static float bigComponent = 0.894427; // 2 / sqrt(5)

    const int2   side = cellSides[index];
    const float3 tmp = float3(side.x * smallComponent, side.y * smallComponent, bigComponent);
    return float3(orientation.x * tmp.x, orientation.y * tmp.y, orientation.z * tmp.z);
}

float3 GetReprojSideDirection(int index, int3 orientation)
{
    const int2 side = cellSides[index];
    return float3(orientation.x * side.x, orientation.y * side.y, 0);
}

float CalculateOcclusion(const LPV_CHANNEL_T blockingPotentialSH, const LPV_CHANNEL_T directionSH)
{
    #if defined(BLOCKING_POTENTIAL) && !defined(FIRST_STEP)
    return LPVMath::EvaluateOcclusion(blockingPotentialSH, directionSH, _OcclusionAmplification);
    #else
    return 1;
    #endif
}

LPV_CHANNEL_T EvalPropagatedSH(const LPV_CHANNEL_T neighborSH, const LPV_CHANNEL_T evalDirectionSH, const LPV_CHANNEL_T cosineLobeSH)
{
    return max(0, dot(neighborSH, evalDirectionSH)) * cosineLobeSH;
}

float EvalPropagatedSkyOcclusion(const LPV_CHANNEL_T evalDirectionSH, const LPV_CHANNEL_T cosineLobeSH)
{
    return max(0, dot(evalDirectionSH, cosineLobeSH));
}

LPV_CHANNEL_T FetchSourceSH(const TYPED_TEXTURE3D(LPV_CHANNEL_T, tex), const uint3 neighbourID)
{
    return tex[neighbourID];
}

float FetchSkyOcclusion(const TYPED_TEXTURE3D(float, tex), const uint3 neighbourID)
{
    return tex[neighbourID];
}

LPV_CHANNEL_T FetchBlockingPotential(const float3 neighbourID)
{
    const float3 uv = neighbourID / LPV::GetGridSize();
    return SAMPLE_TEXTURE3D_LOD(_BlockingPotentialSH, sampler_TrilinearClamp, uv, 0);
}

void AccumulateSH(RWTexture3D<LPV_CHANNEL_T> destination, const TYPED_TEXTURE3D(LPV_CHANNEL_T, source), const uint3 cellID, const LPV_CHANNEL_T valueSH)
{
    destination[cellID] = FetchSourceSH(source, cellID) + valueSH;
}

void AccumulateSkyOcclusion(RWTexture3D<float> destination, const TYPED_TEXTURE3D(float, source), const uint3 cellID, const float value)
{
    destination[cellID] = saturate(FetchSkyOcclusion(source, cellID) + value);
}

[numthreads(THREAD_GROUP_SIZE, THREAD_GROUP_SIZE, THREAD_GROUP_SIZE)]
void CS(const uint3 dispatchThreadID : SV_DispatchThreadID)
{
    const float3 cellID = dispatchThreadID.xyz;
    if (any(cellID >= LPV::GetGridSize()))
    {
        return;
    }

    LPV_CHANNEL_T redSH = 0;
    LPV_CHANNEL_T greenSH = 0;
    LPV_CHANNEL_T blueSH = 0;
    #ifdef SKY_OCCLUSION
    float skyOcclusion = 0;
    #endif

    for (uint neighbour = 0; neighbour < DIRECTIONS_COUNT; ++neighbour)
    {
        const float3 mainDirection = directions[neighbour];
        const float3 neighbourID = cellID - mainDirection;
        if (any(neighbourID < 0) || any(neighbourID >= LPV::GetGridSize()))
        {
            continue;
        }

        const LPV_CHANNEL_T neighbourRedSH = FetchSourceSH(_SourceRedSH, neighbourID);
        const LPV_CHANNEL_T neighbourGreenSH = FetchSourceSH(_SourceGreenSH, neighbourID);
        const LPV_CHANNEL_T neighbourBlueSH = FetchSourceSH(_SourceBlueSH, neighbourID);

        if (dot(neighbourRedSH, neighbourRedSH) != 0 || dot(neighbourGreenSH, neighbourGreenSH) != 0 || dot(neighbourBlueSH, neighbourBlueSH) != 0)
        {
            const static float directFaceSubtendedSolidAngle = 0.4006696846f * INV_PI / 2;
            const static float sideFaceSubtendedSolidAngle = 0.4234413544f * INV_PI / 3;

            #if DIRECT_FACE_CONTRIBUTION
            for (uint sideFace = 0; sideFace < 4; ++sideFace)
            {
                const float3        evalDirection = GetEvalSideDirection(sideFace, mainDirection);
                const LPV_CHANNEL_T evalDirectionSH = LPVMath::DirToSH(evalDirection);
                const LPV_CHANNEL_T blockingPotentialSH = FetchBlockingPotential(neighbourID + evalDirection * 0.5);
                const float3        reprojDirection = GetReprojSideDirection(sideFace, mainDirection);
                LPV_CHANNEL_T       reprojDirectionCosineLobeSH = LPVMath::DirToCosineLobe(reprojDirection);
                reprojDirectionCosineLobeSH *= sideFaceSubtendedSolidAngle * CalculateOcclusion(blockingPotentialSH, evalDirectionSH);

                redSH += EvalPropagatedSH(neighbourRedSH, evalDirectionSH, reprojDirectionCosineLobeSH);
                greenSH += EvalPropagatedSH(neighbourGreenSH, evalDirectionSH, reprojDirectionCosineLobeSH);
                blueSH += EvalPropagatedSH(neighbourBlueSH, evalDirectionSH, reprojDirectionCosineLobeSH);

                #ifdef SKY_OCCLUSION
                const float neighborSkyOcclusion = FetchSkyOcclusion(_SourceSkyOcclusion, neighbourID);
                skyOcclusion += neighborSkyOcclusion * EvalPropagatedSkyOcclusion(evalDirectionSH, reprojDirectionCosineLobeSH);
                #endif
            }
            #endif

            #if SIDE_FACE_CONTRIBUTION
            {
                const LPV_CHANNEL_T mainDirectionSH = LPVMath::DirToSH(mainDirection);
                const LPV_CHANNEL_T blockingPotentialSH = FetchBlockingPotential(neighbourID + mainDirection * 0.5);
                LPV_CHANNEL_T       mainDirectionCosineLobeSH = LPVMath::DirToCosineLobe(mainDirection);
                mainDirectionCosineLobeSH *= directFaceSubtendedSolidAngle * CalculateOcclusion(blockingPotentialSH, mainDirectionSH);

                redSH += EvalPropagatedSH(neighbourRedSH, mainDirectionSH, mainDirectionCosineLobeSH);
                greenSH += EvalPropagatedSH(neighbourGreenSH, mainDirectionSH, mainDirectionCosineLobeSH);
                blueSH += EvalPropagatedSH(neighbourBlueSH, mainDirectionSH, mainDirectionCosineLobeSH);

                #ifdef SKY_OCCLUSION
                const float neighborSkyOcclusion = FetchSkyOcclusion(_SourceSkyOcclusion, neighbourID);
                skyOcclusion += neighborSkyOcclusion * EvalPropagatedSkyOcclusion(mainDirectionSH, mainDirectionCosineLobeSH);
                #endif
            }
            #endif
        }
    }

    AccumulateSH(_DestinationRedSH, _SourceRedSH, cellID, _Intensity * redSH);
    AccumulateSH(_DestinationGreenSH, _SourceGreenSH, cellID, _Intensity * greenSH);
    AccumulateSH(_DestinationBlueSH, _SourceBlueSH, cellID, _Intensity * blueSH);

    #ifdef SKY_OCCLUSION
    AccumulateSkyOcclusion(_DestinationSkyOcclusion, _SourceSkyOcclusion, cellID, _SkyOcclusionPropagationIntensity * skyOcclusion);
    #endif
}