#pragma kernel CS

#pragma use_dxc

#pragma multi_compile_local _ MAIN_PASS FALSE_NEGATIVE_PASS
#pragma multi_compile_local _ DISABLE_OCCLUSION_CULLING

#include_with_pragmas "Packages/com.deltation.aaaa-rp/Shaders/Debugging/GPUCullingDebugPragma.hlsl"

#include "Packages/com.deltation.aaaa-rp/Runtime/Meshlets/AAAAMeshletComputeShaders.cs.hlsl"
#include "Packages/com.deltation.aaaa-rp/ShaderLibrary/Debug/GPUCullingDebug.hlsl"

#define THREAD_GROUP_SIZE GPUINSTANCE_CULLING_THREAD_GROUP_SIZE

#include "Packages/com.deltation.aaaa-rp/ShaderLibrary/Core.hlsl"
#include "Packages/com.deltation.aaaa-rp/ShaderLibrary/Compute/IndirectArgs.hlsl"
#include "Packages/com.deltation.aaaa-rp/ShaderLibrary/Math.hlsl"
#include "Packages/com.deltation.aaaa-rp/ShaderLibrary/VisibilityBuffer/Instances.hlsl"
#include "Packages/com.deltation.aaaa-rp/ShaderLibrary/VisibilityBuffer/GPUCullingContexts.hlsl"
#include "Packages/com.deltation.aaaa-rp/ShaderLibrary/VisibilityBuffer/Meshlets.hlsl"
#define RW_OCCLUSION_CULLING_INSTANCE_VISIBILITY_MASK
#include "Packages/com.deltation.aaaa-rp/ShaderLibrary/VisibilityBuffer/OcclusionCulling.hlsl"

ConstantBuffer<GPUCullingContextArray> _CullingContexts;

ByteAddressBuffer _InstanceIndices;
uint              _InstanceIndicesCount;

RWStructuredBuffer<AAAAMeshletListBuildJob> _Jobs;
RWByteAddressBuffer                         _JobCounter;

[numthreads(THREAD_GROUP_SIZE, 1, 1)]
void CS(const uint3 dispatchThreadID : SV_DispatchThreadID, const uint3 groupID : SV_GroupID)
{
    const uint i = dispatchThreadID.x;
    UNITY_BRANCH
    if (i >= _InstanceIndicesCount)
    {
        return;
    }

    const uint instanceID = _InstanceIndices.Load(i << 2);

    #if defined(MAIN_PASS)
    UNITY_BRANCH
    if (!OcclusionCulling::WasInstanceVisibleLastFrame(instanceID))
    {
        return;
    }
    #endif

    const AAAAInstanceData  instanceData = PullInstanceData(instanceID);
    const uint              contextIndex = groupID.y;
    const GPUCullingContext cullingContext = _CullingContexts.Items[contextIndex];

    UNITY_BRANCH
    if ((instanceData.PassMask & cullingContext.PassMask) == 0)
    {
        return;
    }

    const AABB             aabbOS = AABB::Create(instanceData.AABBMin.xyz, instanceData.AABBMax.xyz);
    const AABB             aabbWS = TransformAABB(aabbOS, instanceData.ObjectToWorldMatrix);
    const float4           boundingSphereWS = AABBToBoundingSphere(aabbWS);
    const BoundingSquareSS boundingSquareSS = OcclusionCulling::ComputeScreenSpaceBoundingSquare(aabbWS, cullingContext.ViewProjectionMatrix);

    UNITY_BRANCH
    if (!FrustumVsSphereCulling(cullingContext.FrustumPlanes, boundingSphereWS) ||
        !LightSphereCulling(cullingContext.CullingSphereLS, cullingContext.ViewMatrix, boundingSphereWS))
    {
        OcclusionCulling::MarkInvisibleThisFrame(instanceID);
        GPUCullingDebug::OnCulled(boundingSquareSS, AAAAGPUCULLINGDEBUGGRANULARITY_INSTANCE, AAAAGPUCULLINGDEBUGTYPE_FRUSTUM);
        return;
    }

    #if defined(FALSE_NEGATIVE_PASS)

    #if !defined(DISABLE_OCCLUSION_CULLING)
    UNITY_BRANCH
    if (!OcclusionCulling::IsVisible(boundingSquareSS))
    {
        OcclusionCulling::MarkInvisibleThisFrame(instanceID);
        GPUCullingDebug::OnCulled(boundingSquareSS, AAAAGPUCULLINGDEBUGGRANULARITY_INSTANCE, AAAAGPUCULLINGDEBUGTYPE_OCCLUSION);
        return;
    }
    #endif

    UNITY_BRANCH
    if (OcclusionCulling::WasInstanceVisibleThisFrame(instanceID))
    {
        return;
    }
    
    #endif

    const uint totalMeshLODCount = instanceData.TotalMeshLODCount;
    const uint jobCount = ceil((float)totalMeshLODCount / MAX_LODNODES_PER_THREAD_GROUP);
    uint       jobWriteOffset;
    _JobCounter.InterlockedAdd(contextIndex * IndirectArgs::DispatchArgsStride(), jobCount, jobWriteOffset);

    for (uint jobIndex = 0; jobIndex < jobCount; ++jobIndex)
    {
        const uint offset = jobIndex * MAX_LODNODES_PER_THREAD_GROUP;
        const uint jobsLeft = totalMeshLODCount - offset;
        const uint count = min(jobsLeft, MAX_LODNODES_PER_THREAD_GROUP);

        AAAAMeshletListBuildJob job = (AAAAMeshletListBuildJob)0;
        job.InstanceID = instanceID,
        job.MeshLODNodeOffset = offset;
        job.MeshLODNodeCount = count;
        // TODO: use the culling context's offset
        _Jobs[jobWriteOffset + jobIndex] = job;
    }
}