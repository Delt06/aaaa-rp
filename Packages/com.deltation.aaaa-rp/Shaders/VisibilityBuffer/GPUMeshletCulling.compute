#pragma kernel CS

#pragma use_dxc

#pragma multi_compile_local _ MAIN_PASS FALSE_NEGATIVE_PASS
#pragma multi_compile_local _ DISABLE_OCCLUSION_CULLING

#include_with_pragmas "Packages/com.deltation.aaaa-rp/ShaderLibrary/Bindless.hlsl"
#include_with_pragmas "Packages/com.deltation.aaaa-rp/Shaders/Debugging/GPUCullingDebugPragma.hlsl"

#include "Packages/com.deltation.aaaa-rp/ShaderLibrary/Debug/GPUCullingDebug.hlsl"

#if defined(MAIN_PASS) || defined(FALSE_NEGATIVE_PASS)
#define OCCLUSION_CULLING_ON
#endif

#include "Packages/com.deltation.aaaa-rp/Runtime/Meshlets/AAAAMeshletComputeShaders.cs.hlsl"
#define THREAD_GROUP_SIZE GPUMESHLET_CULLING_THREAD_GROUP_SIZE

#include "Packages/com.deltation.aaaa-rp/ShaderLibrary/Core.hlsl"
#include "Packages/com.deltation.aaaa-rp/ShaderLibrary/Math.hlsl"
#include "Packages/com.deltation.aaaa-rp/ShaderLibrary/VisibilityBuffer/Instances.hlsl"
#include "Packages/com.deltation.aaaa-rp/ShaderLibrary/VisibilityBuffer/GPUCullingContexts.hlsl"
#include "Packages/com.deltation.aaaa-rp/ShaderLibrary/VisibilityBuffer/Materials.hlsl"
#include "Packages/com.deltation.aaaa-rp/ShaderLibrary/VisibilityBuffer/Meshlets.hlsl"
#include "Packages/com.deltation.aaaa-rp/ShaderLibrary/VisibilityBuffer/MeshletCulling.hlsl"
#define RW_OCCLUSION_CULLING_INSTANCE_VISIBILITY_MASK
#include "Packages/com.deltation.aaaa-rp/ShaderLibrary/VisibilityBuffer/OcclusionCulling.hlsl"

ConstantBuffer<GPUCullingContextArray> _CullingContexts;

uint _CullingContextIndex;
uint _MaxMeshletRenderRequestsPerList;

ByteAddressBuffer _SourceMeshletsCounter;
ByteAddressBuffer _SourceMeshlets;

RWByteAddressBuffer _DestinationMeshletsCounter;
uint                _DestinationMeshletsCounterOffset;
uint                _DestinationMeshletsCounterStride;
RWByteAddressBuffer _DestinationMeshlets;

bool Culling(const GPUCullingContext cullingContext, const AAAAInstanceData instanceData, const uint rendererListID, const AAAAMeshlet meshletData)
{
    const float4           boundingSphereWS = TransformBoundingSphere(meshletData.BoundingSphere, instanceData.ObjectToWorldMatrix);
    const BoundingSquareSS boundingSquareSS = OcclusionCulling::ComputeScreenSpaceBoundingSquare(boundingSphereWS, cullingContext.ViewProjectionMatrix);

    bool result = false;

    UNITY_BRANCH
    if (FrustumVsSphereCulling(cullingContext.FrustumPlanes, boundingSphereWS) &&
        LightSphereCulling(cullingContext.CullingSphereLS, cullingContext.ViewMatrix, boundingSphereWS))
    {
        const float3 viewForwardDirWS = -GetViewForwardDir(cullingContext.ViewMatrix);
        const bool   isPerspective = cullingContext.CameraIsPerspective;

        UNITY_BRANCH
        if (rendererListID & AAAARENDERERLISTID_CULL_OFF ||
            ConeCulling(cullingContext.CameraPosition.xyz, viewForwardDirWS, isPerspective, instanceData, meshletData))
        {
            #if defined(FALSE_NEGATIVE_PASS) && !defined(DISABLE_OCCLUSION_CULLING)
        
            UNITY_BRANCH
            if (OcclusionCulling::IsVisible(boundingSquareSS))
            {
                result = true;
            }
            else
            {
                GPUCullingDebug::OnCulled(boundingSquareSS, AAAAGPUCULLINGDEBUGGRANULARITY_MESHLET, AAAAGPUCULLINGDEBUGTYPE_OCCLUSION);
            }
        
            #else

            result = true;

            #endif
        }
        else
        {
            GPUCullingDebug::OnCulled(boundingSquareSS, AAAAGPUCULLINGDEBUGGRANULARITY_MESHLET, AAAAGPUCULLINGDEBUGTYPE_CONE);
        }
    }
    else
    {
        GPUCullingDebug::OnCulled(boundingSquareSS, AAAAGPUCULLINGDEBUGGRANULARITY_MESHLET, AAAAGPUCULLINGDEBUGTYPE_FRUSTUM);
    }

    return result;
}

uint GetRendererListID(const AAAAInstanceData instanceData, const AAAAMaterialData materialData)
{
    uint rendererListID = materialData.RendererListID;

    if (instanceData.Flags & AAAAINSTANCEFLAGS_FLIP_WINDING_ORDER &&
        !(rendererListID & AAAARENDERERLISTID_CULL_OFF))
    {
        if (rendererListID & AAAARENDERERLISTID_CULL_FRONT)
        {
            rendererListID &= ~AAAARENDERERLISTID_CULL_FRONT;
        }
        else
        {
            rendererListID |= AAAARENDERERLISTID_CULL_FRONT;
        }
    }

    return rendererListID;
}

[numthreads(THREAD_GROUP_SIZE, 1, 1)]
void CS(const uint3 dispatchThreadID : SV_DispatchThreadID)
{
    const uint meshletsCount = _SourceMeshletsCounter.Load(_CullingContextIndex * 4);
    const uint sourceMeshletID = dispatchThreadID.x;

    UNITY_BRANCH
    if (sourceMeshletID >= meshletsCount)
    {
        return;
    }

    const GPUCullingContext cullingContext = _CullingContexts.Items[_CullingContextIndex];

    const AAAAMeshletRenderRequest meshletRenderRequest = PullMeshletRenderRequest(_SourceMeshlets, sourceMeshletID);
    const AAAAInstanceData         instanceData = PullInstanceData(meshletRenderRequest.InstanceID);
    const AAAAMaterialData         materialData = PullMaterialData(instanceData.MaterialIndex);
    const uint                     rendererListID = GetRendererListID(instanceData, materialData);
    const AAAAMeshlet              meshlet = PullMeshletData(meshletRenderRequest.MeshletID);

    if (!Culling(cullingContext, instanceData, rendererListID, meshlet))
    {
        return;
    }

    uint       localRenderRequestWriteOffset = 0;
    const uint counterAddress = _DestinationMeshletsCounterStride * rendererListID + _DestinationMeshletsCounterOffset;
    _DestinationMeshletsCounter.InterlockedAdd(counterAddress, 1, localRenderRequestWriteOffset);

    // TODO: use the culling context's offset
    const uint renderRequestIndex = localRenderRequestWriteOffset + rendererListID * _MaxMeshletRenderRequestsPerList;
    StoreMeshletRenderRequest(_DestinationMeshlets, renderRequestIndex, meshletRenderRequest);
    #ifdef OCCLUSION_CULLING_ON
    OcclusionCulling::MarkVisibleThisFrame(meshletRenderRequest.InstanceID);
    #endif
}